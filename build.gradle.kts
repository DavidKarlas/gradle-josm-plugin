import groovy.lang.GroovySystem
import org.jetbrains.dokka.DokkaConfiguration
import org.jetbrains.dokka.gradle.DokkaTask
import org.jetbrains.kotlin.gradle.plugin.KotlinSourceSet
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.openstreetmap.josm.gradle.plugin.PublishGitlabPackageAsRelease
import org.openstreetmap.josm.gradle.plugin.GitDescriber
import org.openstreetmap.josm.gradle.plugin.Versions
import org.openstreetmap.josm.gradle.plugin.logCoverage
import org.openstreetmap.josm.gradle.plugin.logSkippedTasks
import org.openstreetmap.josm.gradle.plugin.logTaskDuration
import java.net.URL

plugins {
  id("com.gradle.plugin-publish").version("0.10.1")
  id("com.github.ben-manes.versions").version("0.20.0")
  id("org.jetbrains.dokka").version("0.9.17")

  jacoco
  maven
  `java-gradle-plugin`
  `maven-publish`
}

apply(plugin = "kotlin")


// Logging
gradle.taskGraph.logTaskDuration()
logSkippedTasks()
allprojects {
  afterEvaluate {
    tasks.withType(JacocoReport::class) {
      logCoverage()
    }
  }
  group = "org.openstreetmap.josm"
  version = GitDescriber(rootProject.projectDir).describe(trimLeading = true)

  repositories {
    jcenter()
  }

  tasks.withType(KotlinCompile::class) {
    kotlinOptions.jvmTarget = "1.8"
  }

  val jacocoTask = tasks.withType(JacocoReport::class).findByName("jacocoTestReport")
  tasks.withType(Test::class) {
    useJUnitPlatform()
    jacocoTask?.also { this.finalizedBy(it) }
  }

  this.extensions.findByType(JacocoPluginExtension::class)?.apply {
    toolVersion = Versions.jacoco
  }
}

afterEvaluate {
  // use Javadoc generated by Dokka
  tasks.named<Jar>("publishPluginJavaDocsJar") {
    setDependsOn(dependsOn - tasks.javadoc)
    from(tasks.dokka)
  }
}

// Reuse the kotlin sources from the "buildSrc" project in the plugin itself
sourceSets.main.get().withConvention(KotlinSourceSet::class) {
  kotlin.srcDir("$projectDir/buildSrc/src/dual/kotlin")
}
// Add dependencies of "buildSrc" sources
val mainProject = this
project(":buildSrc").afterEvaluate {
  this.configurations.getByName(this.sourceSets.getByName("dual").implementationConfigurationName).dependencies.forEach {
    // Add all `dual` dependencies of the `buildSrc` project to `main` source set of this project
    mainProject.dependencies.implementation(it)
  }
}

dependencies {
  implementation(localGroovy())
  implementation(kotlin("stdlib-jdk8", Versions.kotlin))
  implementation("com.squareup.okhttp3", "okhttp", Versions.okhttp)
  implementation("com.beust","klaxon", Versions.klaxon)
  constraints {
    implementation(kotlin("reflect", Versions.kotlin)) { because("Align Kotlin version used by Klaxon dependencies with the one used by gradle-josm-plugin.") }
  }
  implementation("com.fasterxml.jackson.module", "jackson-module-kotlin", Versions.jackson)
  implementation("com.fasterxml.jackson.dataformat", "jackson-dataformat-yaml", Versions.jackson)
  implementation("com.vladsch.flexmark:flexmark:${Versions.flexmark}")

  testImplementation("org.junit.jupiter", "junit-jupiter-api", Versions.junit)
  testImplementation("com.github.tomakehurst","wiremock",Versions.wiremock)
  testImplementation("ru.lanwen.wiremock", "wiremock-junit5", Versions.wiremockJunit5)
  testRuntimeOnly("org.junit.jupiter", "junit-jupiter-engine", Versions.junit)
  testImplementation(kotlin("reflect"))
}

// Configure "dokka" task
val dokkaTask = tasks.withType(DokkaTask::class).getByName("dokka") {
  outputFormat = "html"
  outputDirectory = "$buildDir/docs/kdoc"
}

gradle.projectsEvaluated {
  project(":buildSrc").sourceSets.getByName("dual").withConvention(KotlinSourceSet::class) {
    dokkaTask.sourceDirs = dokkaTask.sourceDirs.plus(this.kotlin.srcDirs)
  }
}

// Configure all Dokka tasks
tasks.withType(DokkaTask::class) {
  includes = listOfNotNull("src/main/kotlin/packages.md")
  jdkVersion = 8
  skipEmptyPackages = false

  externalDocumentationLinks.add(DokkaConfiguration.ExternalDocumentationLink.Builder(URL("https://docs.gradle.org/${project.gradle.gradleVersion}/javadoc/")).build())
  externalDocumentationLinks.add(DokkaConfiguration.ExternalDocumentationLink.Builder(URL("http://docs.groovy-lang.org/${GroovySystem.getVersion()}/html/api/")).build())
}

// for the plugin-publish (publish to plugins.gradle.org)
pluginBundle {
  website = "https://gitlab.com/floscher/gradle-josm-plugin#readme"
  vcsUrl = "https://gitlab.com/floscher/gradle-josm-plugin.git"
  description = "This plugin helps with developing for the JOSM project."
  tags = listOf("josm", "openstreetmap", "osm")

  plugins.create("josmPlugin") {
    id = project.group.toString()
    displayName = "Gradle JOSM plugin"
  }
}
// for the java-gradle-plugin (local publishing)
gradlePlugin {
  plugins.create("josmPlugin") {
    id = project.group.toString()
    implementationClass = "org.openstreetmap.josm.gradle.plugin.JosmPlugin"
  }
}

val buildDirRepo = publishing.repositories.maven("$buildDir/maven") {
  name = "buildDir"
}

val ciJobToken: String? = System.getenv("CI_JOB_TOKEN")
val projectId: String? = System.getenv("CI_PROJECT_ID")
val gitlabRepo = if (ciJobToken != null && projectId != null) {
  publishing.repositories.maven("https://gitlab.com/api/v4/projects/$projectId/packages/maven") {
    name = "gitlab"
    credentials(HttpHeaderCredentials::class) {
      name = "Job-Token"
      value = ciJobToken
    }
    authentication {
      create("auth", HttpHeaderAuthentication::class)
    }
    createPublishToTask(this)
  }
} else null

val awsAccessKeyId: String? = System.getenv("AWS_ACCESS_KEY_ID")
val awsSecretAccessKey: String? = System.getenv("AWS_SECRET_ACCESS_KEY")
val s3Repo = if (awsAccessKeyId == null || awsSecretAccessKey == null) {
  logger.lifecycle(
    "Note: Set the environment variables AWS_ACCESS_KEY_ID ({} set) and AWS_SECRET_ACCESS_KEY ({} set) to be able to publish the plugin to s3://gradle-josm-plugin .",
    if (awsAccessKeyId == null) { "not" } else { "is" },
    if (awsSecretAccessKey == null) { "not" } else { "is" }
  )
  null
} else {
  publishing.repositories.maven("s3://gradle-josm-plugin") {
    name = "s3"
    credentials(AwsCredentials::class.java) {
      accessKey = System.getenv("AWS_ACCESS_KEY_ID")
      secretKey = System.getenv("AWS_SECRET_ACCESS_KEY")
    }
    createPublishToTask(this)
  }
}

gradle.projectsEvaluated {
  tasks.withType(PublishToMavenRepository::class) {
    if (repository == buildDirRepo) {
      tasks.withType(Test::class).forEach {
        it.dependsOn(this)
      }
    }

    doLast {
      logger.lifecycle("""
        Published artifact:

           to URL: ${repository.url}
            Group: ${publication.groupId}
               ID: ${publication.artifactId}
          Version: ${publication.version}

      """.trimIndent())
    }
  }
}

fun createPublishToTask(repo: MavenArtifactRepository) {
  gradle.projectsEvaluated {
    val publishTask = tasks.create("publishTo${repo.name.capitalize()}") {
      group = "Publishing"
      description = "Publishes all Maven publications produced by this project to the '${repo.name}' Maven repository at ${repo.url}"
      dependsOn(tasks.withType(PublishToMavenRepository::class).filter { it.repository == repo })
    }
    if (repo == gitlabRepo) {
      publishTask.finalizedBy(
        tasks.create(
          "releaseToGitlab",
          PublishGitlabPackageAsRelease::class,
          project.version.toString(),
          listOf("org/openstreetmap/josm/gradle-josm-plugin", "org/openstreetmap/josm/langconv")
        )
      )
    }
  }
}
