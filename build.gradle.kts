import com.github.benmanes.gradle.versions.updates.DependencyUpdatesTask
import org.jetbrains.dokka.gradle.DokkaTask
import org.jetbrains.dokka.gradle.GradleSourceRootImpl
import org.jetbrains.kotlin.gradle.plugin.KotlinSourceSet
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.openstreetmap.josm.gradle.plugin.GitDescriber
import org.openstreetmap.josm.gradle.plugin.Versions
import org.openstreetmap.josm.gradle.plugin.api.gitlab.gitlabRepository
import org.openstreetmap.josm.gradle.plugin.logCoverage
import org.openstreetmap.josm.gradle.plugin.logSkippedTasks
import org.openstreetmap.josm.gradle.plugin.logTaskDuration
import org.openstreetmap.josm.gradle.plugin.logPublishedMavenArtifacts
import org.openstreetmap.josm.gradle.plugin.task.gitlab.ReleaseToGitlab
import java.net.URL

plugins {
  id("com.gradle.plugin-publish").version("0.10.1")
  id("com.github.ben-manes.versions").version("0.27.0")
  id("org.jetbrains.dokka").version("0.10.0")

  jacoco
  `java-gradle-plugin`
  `maven-publish`
}

apply(plugin = "kotlin")
apply(plugin = "kotlinx-serialization")


// Logging
gradle.taskGraph.logPublishedMavenArtifacts()
gradle.taskGraph.logTaskDuration()
logSkippedTasks()

// Little hack to disable logging in demo project without including the setting of these properties in that subproject.
// Since that logging is also enabled for the rootProject, leaving these properties enabled would lead to duplicate log messages.
project.childProjects["demo"]?.afterEvaluate {
  fun Class<*>.setBooleanField(obj: Any, fieldName: String, value: Boolean) = this.declaredFields.singleOrNull { it.name ==  fieldName}!!.also {
    it.isAccessible = true
    it.setBoolean(obj, value)
  }

  val josmExtensionClass = buildscript.classLoader.loadClass("org.openstreetmap.josm.gradle.plugin.config.JosmPluginExtension")
  this.extensions.getByName("josm").apply {
    josmExtensionClass.setBooleanField(this, "logSkippedTasks", false)
    josmExtensionClass.setBooleanField(this, "logTaskDuration", false)
  }
}

allprojects {
  afterEvaluate {
    tasks.withType(JacocoReport::class) {
      logCoverage()
    }
  }
  group = "org.openstreetmap.josm"
  version = GitDescriber(rootProject.projectDir).describe(trimLeading = true)

  repositories {
    jcenter()
  }

  tasks.withType(KotlinCompile::class) {
    kotlinOptions.jvmTarget = "1.8"
  }

  val jacocoTask = tasks.withType(JacocoReport::class).findByName("jacocoTestReport")
  tasks.withType(Test::class) {
    useJUnitPlatform()
    jacocoTask?.also { this.finalizedBy(it) }
  }

  this.extensions.findByType(JacocoPluginExtension::class)?.apply {
    toolVersion = Versions.jacoco
  }
}

afterEvaluate {
  // use Javadoc generated by Dokka
  tasks.named<Jar>("publishPluginJavaDocsJar") {
    setDependsOn(dependsOn - tasks.javadoc)
    from(tasks.dokka)
  }
}

val dualSourceSet = sourceSets.create("dual") {
  java.setSrcDirs(setOf<Any>())
  withConvention(KotlinSourceSet::class) {
    kotlin.setSrcDirs(setOf(File(projectDir, "buildSrc/src/${this.name}/kotlin")))
  }
  resources.setSrcDirs(setOf(File(projectDir, "buildSrc/src/${this.name}/resources")))
}

tasks.jar {
  from(dualSourceSet.output)
}

dependencies {
  add(dualSourceSet.implementationConfigurationName, gradleApi())
  add(dualSourceSet.implementationConfigurationName, "org.eclipse.jgit:org.eclipse.jgit:${Versions.jgit}")
  add(dualSourceSet.implementationConfigurationName, kotlin("stdlib", Versions.kotlin))
  add(dualSourceSet.implementationConfigurationName, (dependencies.create("org.jetbrains.kotlinx:kotlinx-serialization-runtime:${Versions.kotlinSerialization}") as ModuleDependency).also {
    //it.exclude("org.jetbrains.kotlinx")
  })
  implementation(dualSourceSet.output)
  testImplementation(configurations.getByName(dualSourceSet.implementationConfigurationName))

  implementation(localGroovy())
  implementation(kotlin("stdlib", Versions.kotlin))
  implementation("com.squareup.okhttp3", "okhttp", Versions.okhttp)
  implementation("com.beust","klaxon", Versions.klaxon)
  constraints {
    implementation(kotlin("reflect", Versions.kotlin)) { because("Align Kotlin version used by Klaxon dependencies with the one used by gradle-josm-plugin.") }
  }
  implementation("com.fasterxml.jackson.module", "jackson-module-kotlin", Versions.jackson)
  implementation("com.fasterxml.jackson.dataformat", "jackson-dataformat-yaml", Versions.jackson)
  implementation("com.vladsch.flexmark:flexmark:${Versions.flexmark}")

  testImplementation("org.junit.jupiter", "junit-jupiter-api", Versions.junit)
  testImplementation("com.github.tomakehurst","wiremock",Versions.wiremock)
  testImplementation("ru.lanwen.wiremock", "wiremock-junit5", Versions.wiremockJunit5)
  testRuntimeOnly("org.junit.jupiter", "junit-jupiter-engine", Versions.junit)
  testImplementation(kotlin("reflect"))
}

// Configure "dokka" task
val dokkaTask: DokkaTask = tasks.withType(DokkaTask::class).getByName("dokka") {
  outputFormat = "html"
  outputDirectory = "$buildDir/docs/kdoc"
}

// Configure all Dokka tasks
tasks.withType(DokkaTask::class) {
  configuration {
    dualSourceSet.withConvention(KotlinSourceSet::class) {
      sourceRoots.addAll(this.kotlin.srcDirs.map { srcDir -> GradleSourceRootImpl().also { it.path = srcDir.path } })
    }
    includes = listOf("src/main/kotlin/packages.md")
    jdkVersion = 8
    skipEmptyPackages = false

    externalDocumentationLink {
      url = URL("https://docs.gradle.org/${project.gradle.gradleVersion}/javadoc/")
      packageListUrl = URL("https://docs.gradle.org/${project.gradle.gradleVersion}/javadoc/package-list")
    }
    externalDocumentationLink {
      url = URL("https://docs.groovy-lang.org/next/html/api/")
      packageListUrl = URL("http://docs.groovy-lang.org/next/html/api/package-list")
    }
  }
}

// for the plugin-publish (publish to plugins.gradle.org)
pluginBundle {
  website = "https://gitlab.com/floscher/gradle-josm-plugin#readme"
  vcsUrl = "https://gitlab.com/floscher/gradle-josm-plugin.git"
  description = "This plugin helps with developing for the JOSM project."
  tags = listOf("josm", "openstreetmap", "osm")

  plugins.create("josmPlugin") {
    id = project.group.toString()
    displayName = "Gradle JOSM plugin"
  }
}
// for the java-gradle-plugin (local publishing)
gradlePlugin {
  plugins.create("josmPlugin") {
    id = project.group.toString()
    implementationClass = "org.openstreetmap.josm.gradle.plugin.JosmPlugin"
  }
}

val buildDirRepo = publishing.repositories.maven("$buildDir/maven") {
  name = "buildDir"
}

val awsAccessKeyId: String? = System.getenv("AWS_ACCESS_KEY_ID")
val awsSecretAccessKey: String? = System.getenv("AWS_SECRET_ACCESS_KEY")
val s3Repo = if (awsAccessKeyId == null || awsSecretAccessKey == null) {
  logger.lifecycle(
    "Note: If you want to be able to publish the plugin to s3://gradle-josm-plugin , set the environment variables AWS_ACCESS_KEY_ID ({} set) and AWS_SECRET_ACCESS_KEY ({} set).",
    if (awsAccessKeyId == null) { "not" } else { "is" },
    if (awsSecretAccessKey == null) { "not" } else { "is" }
  )
  null
} else {
  publishing.repositories.maven("s3://gradle-josm-plugin") {
    name = "s3"
    credentials(AwsCredentials::class.java) {
      accessKey = awsAccessKeyId
      secretKey = awsSecretAccessKey
    }
  }
}

gradle.projectsEvaluated {
  tasks.withType(Test::class).forEach {
    it.dependsOn(tasks.withType(PublishToMavenRepository::class).filter { it.repository == buildDirRepo })
  }
}

// Create GitLab Maven repository to publish to.
publishing.repositories.gitlabRepository("gitlab", project.logger)

// Create `releaseToGitlab` task that can publish a release based on a Gitlab Maven package for a tag.
val releaseToGitlab = tasks.create(
  "releaseToGitlab",
  ReleaseToGitlab::class,
  { true },
  setOf("org/openstreetmap/josm/gradle-josm-plugin", "org/openstreetmap/josm/langconv")
)

tasks.withType<DependencyUpdatesTask>() {
  rejectVersionIf {
    listOf("release", "final", "ga").none { candidate.version.toLowerCase().contains(it) } && !"^[0-9,.v-]+(-r)?$".toRegex().matches(candidate.version)
  }
}
