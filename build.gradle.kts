import groovy.lang.GroovySystem
import org.codehaus.groovy.runtime.DefaultGroovyMethods.printf
import org.eclipse.jgit.internal.storage.file.FileRepository
import org.eclipse.jgit.lib.Repository
import org.eclipse.jgit.storage.file.FileRepositoryBuilder
import org.gradle.api.internal.artifacts.repositories.layout.MavenRepositoryLayout
import org.eclipse.jgit.api.Git
import org.gradle.api.internal.HasConvention
import org.gradle.kotlin.dsl.resolver.SourcePathProvider
import org.gradle.kotlin.dsl.resolver.buildSrcSourceRootsFilePath
import org.jetbrains.dokka.DokkaConfiguration
import org.jetbrains.dokka.ExternalDocumentationLinkImpl
import org.jetbrains.dokka.gradle.DokkaPlugin
import org.jetbrains.dokka.gradle.DokkaTask
import org.jetbrains.dokka.gradle.SourceRoot
import org.jetbrains.kotlin.gradle.dsl.KotlinCompile
import org.jetbrains.kotlin.gradle.plugin.KotlinSourceSet
import org.openstreetmap.josm.gradle.plugin.GitDescriber

import java.time.Duration
import java.time.Instant
import java.net.URL

buildscript {
  val kotlinVersion: String by project.extra { "1.2.41" }
  repositories {
    jcenter()
  }
  dependencies {
    classpath(kotlin("gradle-plugin", kotlinVersion))
  }
}

plugins {
  id("com.gradle.plugin-publish").version("0.9.10")
  id("com.github.ben-manes.versions").version("0.17.0")
  id("org.jetbrains.dokka").version("0.9.17")

  jacoco
  maven
  eclipse
  `java-gradle-plugin`
  `maven-publish`
}
apply {
  plugin("org.jetbrains.kotlin.jvm")
}

gradle.taskGraph.beforeTask {
  val startTime by this.extra { Instant.now() }
}

gradle.taskGraph.afterTask {
  if (!state.skipped) {
    val startTime: Instant by this.extra
    project.logger.lifecycle(String.format("  üèÅ Finished after %.3f seconds.", Duration.between(startTime, Instant.now()).toMillis() / 1e3))
  }
}

gradle.buildFinished {
  var wereTasksSkipped = false
  tasks.forEach {
    if (it.state.skipped) {
      if (!wereTasksSkipped) {
        it.logger.lifecycle("\nSkipped tasks:")
        wereTasksSkipped = true
      }
      it.logger.lifecycle("‚è≠Ô∏è  :${it.name} (${it.state.skipMessage})")
    }
  }
}
afterEvaluate {
  // use Javadoc generated by Dokka
  tasks["publishPluginJavaDocsJar"].dependsOn.remove(tasks["javadoc"])
  tasks.withType(Jar::class.java)["publishPluginJavaDocsJar"].from(tasks["dokka"])
}

repositories {
  jcenter()
}

// Reuse the kotlin sources from the "buildSrc" project
gradle.projectsEvaluated {
  java.sourceSets["main"].withConvention(KotlinSourceSet::class) {
    project(":buildSrc").java.sourceSets["main"]
      .withConvention(KotlinSourceSet::class) { kotlin.srcDirs }
      .forEach {
        this.kotlin.srcDir(it)
      }
  }
  project(":buildSrc").configurations["implementation"].dependencies.forEach {
    // Add all `implementation` dependencies of the `buildSrc` project
    dependencies.implementation(it)
  }
}

dependencies {
  val kotlinVersion: String by project.extra
  implementation(localGroovy())
  implementation("org.jetbrains.kotlin", "kotlin-stdlib-jdk8", kotlinVersion)
  testImplementation("org.junit.jupiter", "junit-jupiter-api", "5.2.0")
  testRuntimeOnly("org.junit.jupiter", "junit-jupiter-engine", "5.2.0")
}

jacoco {
  toolVersion = "0.8.1"
}

tasks.withType(Test::class.java) {
  useJUnitPlatform()
}
tasks.findByName("jacocoTestReport")?.dependsOn(tasks.findByName("test"))

tasks {
  "dokka"(DokkaTask::class) {
    outputFormat = "html"
    outputDirectory = "$buildDir/docs/kdoc"
    gradle.projectsEvaluated {
      project(":buildSrc").java.sourceSets["main"].withConvention(KotlinSourceSet::class) {
        kotlin.srcDirs.forEach {
          sourceDirs = sourceDirs.toList().plus(it).asIterable()
        }
      }
    }
  }
  withType(DokkaTask::class.java) {
    includes = listOf("src/main/kotlin/packages.md")
    jdkVersion = 8
    skipEmptyPackages = false

    externalDocumentationLinks.add(DokkaConfiguration.ExternalDocumentationLink.Builder(URL("https://docs.gradle.org/${project.gradle.gradleVersion}/javadoc/")).build())
    externalDocumentationLinks.add(DokkaConfiguration.ExternalDocumentationLink.Builder(URL("http://docs.groovy-lang.org/${GroovySystem.getVersion()}/html/api/")).build())
  }
}

group = "org.openstreetmap.josm"


val tmpVersion = GitDescriber(projectDir).describe()
version = if (tmpVersion[0] == 'v') tmpVersion.substring(1) else tmpVersion

// for the plugin-publish (publish to plugins.gradle.org)
pluginBundle {
  website = "https://gitlab.com/floscher/gradle-josm-plugin#readme"
  vcsUrl = "https://gitlab.com/floscher/gradle-josm-plugin.git"
  description = "This plugin helps with developing for the JOSM project."
  tags = listOf("josm", "openstreetmap", "osm")

  plugins.create("josmPlugin") {
    id = project.group.toString()
    displayName = "Gradle JOSM plugin"
  }
}
// for the java-gradle-plugin (local publishing)
gradlePlugin {
  plugins.create("josmPlugin") {
    id = project.group.toString()
    implementationClass = "org.openstreetmap.josm.gradle.plugin.JosmPlugin"
  }
}

publishing {
  repositories {
    maven {
      setUrl("$buildDir/maven")
    }
  }
}
tasks {
  "publish" {
    description = "Deploys the gradle-josm-plugin to a local Maven repository inside the $buildDir"
    doLast {
      logger.lifecycle("Version {} is now deployed to {}", project.version.toString(), publishing.repositories.withType(MavenArtifactRepository::class.java)[0].url)
    }
  }
}

eclipse.project {
  natures(
    "org.eclipse.buildship.core.gradleprojectnature",
    "org.eclipse.jdt.core.javanature",
    "org.jetbrains.kotlin.core.kotlinNature"
  )
  buildCommand("org.eclipse.buildship.core.gradleprojectbuilder")
  buildCommand("org.eclipse.jdt.core.javabuilder")
  buildCommand("org.jetbrains.kotlin.ui.kotlinBuilder")
}
