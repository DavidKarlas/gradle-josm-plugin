package org.openstreetmap.josm.gradle.plugin.setup

import org.gradle.api.Project
import org.gradle.api.tasks.Copy
import org.gradle.api.tasks.Sync
import org.gradle.api.tasks.TaskExecutionException

import org.openstreetmap.josm.gradle.plugin.RunJosmTask

class PluginTaskSetup extends AbstractSetup {
  Project pro

  public void setup() {
    pro.task(
      [type: Copy],
      'updatePluginDependencies',
      {t ->
        doFirst {
          pro.logger.lifecycle 'Copy {} to {}…', source.files, destinationDir
        }
        pro.gradle.projectsEvaluated {
          from pro.configurations.requiredPlugin
          into "${pro.josm.tmpJosmHome}/plugins"
          rename('(.*)-\\.jar', '$1.jar')
        }
      }
    )

    pro.task(
      [type: Sync],
      "renameDistJar",
      {t ->
        from pro.jar.outputs
        into "${pro.buildDir}/dist"
        pro.gradle.projectsEvaluated {
          rename('.*', pro.josm.jarName ? pro.josm.jarName : '$0')
        }
      }
    )
    pro.tasks.jar.finalizedBy pro.renameDistJar

    pro.task(
      [type: Copy, description: 'Puts the plugin-JAR generated by the `jar`-task into `build/.josm/`', dependsOn: [pro.renameDistJar, pro.initJosmPrefs, pro.updatePluginDependencies]],
      'updateCurrentPlugin',
      {t ->
        doFirst {
          pro.logger.lifecycle 'Copy {} to {}…', source.files, destinationDir
        }
        pro.updateJosmPlugins.dependsOn t
        pro.gradle.projectsEvaluated {
          from "${pro.buildDir}/dist"
          into "${pro.josm.tmpJosmHome}/plugins"
        }
      }
    )
  }
}
