buildscript {
  repositories {
    jcenter()
  }
}
plugins {
  id "com.gradle.plugin-publish" version "0.9.9"
  id "com.github.ben-manes.versions" version "0.17.0"
  id "org.jetbrains.kotlin.jvm" version "1.2.10"
  id "org.jetbrains.dokka" version "0.9.15"
  id "groovy"
  id "maven"
  id "eclipse"

  id "java-gradle-plugin"
  id "maven-publish"
}
import org.gradle.api.tasks.javadoc.Groovydoc
import java.time.Instant
import java.time.Duration

gradle.taskGraph.beforeTask { task ->
  task.ext.setProperty("startTime", Instant.now())
}
gradle.taskGraph.afterTask { task, state ->
  if (!state.skipped) {
    project.logger.lifecycle "  üèÅ Finished after " + String.format("%.3f", Duration.between(task.ext.startTime, Instant.now()).toSeconds()) + " seconds."
  }
}

repositories {
  jcenter()
}

dependencies {
  implementation localGroovy()
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

dokka {
  outputFormat = "html"
  outputDirectory = "$buildDir/docs/dokka"
  externalDocumentationLink { url = new URL("https://docs.gradle.org/current/javadoc/") }
  externalDocumentationLink { url = new URL("https://docs.oracle.com/javase/8/docs/api/") }
  externalDocumentationLink { url = new URL("http://docs.groovy-lang.org/latest/html/api/") }
}
tasks.assemble.dependsOn dokka

groovydoc {
  exclude 'org/openstreetmap/josm/gradle/plugin/setup/*' // don't create documentation for the setup package
  links = [
    new Groovydoc.Link('https://docs.gradle.org/current/javadoc/', 'org.gradle'),
    new Groovydoc.Link('https://docs.oracle.com/javase/9/docs/api/', 'java', 'javax'),
    new Groovydoc.Link('http://docs.groovy-lang.org/latest/html/api/', 'groovy')
  ]
}

tasks.withType(Javadoc) {
  failOnError false
}

def versionStream = new ByteArrayOutputStream()
exec {
  commandLine 'git', 'describe', '--dirty', '--always'
  standardOutput versionStream
}
group = 'org.openstreetmap.josm'
version = versionStream.toString().trim()
version = version.charAt(0) == 'v' ? version.substring(1) : version

// for the plugin-publish (publish to plugins.gradle.org)
pluginBundle {
  website = 'https://github.com/floscher/gradle-josm-plugin#readme'
  vcsUrl = 'https://github.com/floscher/gradle-josm-plugin.git'
  description = 'This plugin helps with developing for the JOSM project.'
  tags = ['josm', 'openstreetmap', 'osm']

  plugins {
    josmPlugin {
      id = project.group
      displayName = 'Gradle JOSM plugin'
    }
  }
}
// for the java-gradle-plugin (local publishing)
gradlePlugin {
  plugins {
    josmPlugin {
      id = project.group
      implementationClass = 'org.openstreetmap.josm.gradle.plugin.JosmPlugin'
    }
  }
}

publishing {
  repositories {
    maven {
      url "$buildDir/maven"
    }
  }
}

tasks.publish {
  doLast {
    println "Version " + project.version + " is now deployed!"
  }
}
