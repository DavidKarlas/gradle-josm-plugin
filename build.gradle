plugins {
  id "com.gradle.plugin-publish" version "0.9.9"
  id "groovy"
  id "maven"
  id "eclipse"

  id "java-gradle-plugin"
  id "maven-publish"
}
import org.gradle.api.tasks.javadoc.Groovydoc

dependencies {
  compile localGroovy()
}

groovydoc {
  exclude 'org/openstreetmap/josm/gradle/plugin/*/*' // only create documentation for the root package
  links = [
    new Groovydoc.Link('https://docs.gradle.org/current/javadoc/', 'org.gradle'),
    new Groovydoc.Link('https://docs.oracle.com/javase/8/docs/api/', 'java', 'javax'),
    new Groovydoc.Link('http://docs.groovy-lang.org/latest/html/api/', 'groovy')
  ]
}

def versionStream = new ByteArrayOutputStream()
exec {
  commandLine 'git', 'describe', '--dirty', '--always'
  standardOutput versionStream
}
group = 'org.openstreetmap.josm'
version = versionStream.toString().trim()
version = version.charAt(0) == 'v' ? version.substring(1) : version

// for the plugin-publish (publish to plugins.gradle.org)
pluginBundle {
  website = 'https://floscher.github.io/gradle-josm-plugin'
  vcsUrl = 'https://github.com/floscher/gradle-josm-plugin'
  description = 'This plugin helps with developing for the JOSM project.'
  tags = ['josm', 'openstreetmap', 'osm']

  plugins {
    josmPlugin {
      id = project.group
      displayName = 'Gradle JOSM plugin'
    }
  }
}
// for the java-gradle-plugin (local publishing)
gradlePlugin {
  plugins {
    josmPlugin {
      id = project.group
      implementationClass = 'org.openstreetmap.josm.gradle.plugin.JosmPlugin'
    }
  }
}

publishing {
  repositories {
    maven {
      url "$buildDir/maven"
    }
  }
}

tasks.publish {
  doLast {
    println "Version " + project.version + " is now deployed!"
  }
}
