plugins {
  id "com.gradle.plugin-publish" version "0.9.7"
}
import org.gradle.api.tasks.Upload
import org.gradle.api.tasks.javadoc.Groovydoc

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'eclipse'

dependencies {
  compile gradleApi()
  compile localGroovy()
}

groovydoc {
  links = [
    new Groovydoc.Link('https://docs.gradle.org/current/javadoc/', 'org.gradle'),
    new Groovydoc.Link('https://docs.oracle.com/javase/8/docs/api/', 'java', 'javax'),
    new Groovydoc.Link('http://docs.groovy-lang.org/latest/html/api/', 'groovy')
  ]
}

def versionStream = new ByteArrayOutputStream()
exec {
  commandLine 'git', 'describe', '--dirty', '--always'
  standardOutput versionStream
}
group = 'org.openstreetmap.josm.gradle.plugin'
version = versionStream.toString().trim()
version = version.charAt(0) == 'v' ? version.substring(1) : version

pluginBundle {
  website = 'https://floscher.github.io/gradle-josm-plugin'
  vcsUrl = 'https://github.com/floscher/gradle-josm-plugin'
  description = 'This plugin helps with developing for the JOSM project.'
  tags = ['josm', 'openstreetmap', 'osm']

  plugins {
    josmPlugin {
      id = 'org.openstreetmap.josm.gradle.plugin'
      displayName = 'Gradle JOSM plugin'
    }
  }
}

task deploy(type: Upload) {
  configuration = configurations.archives
  repositories {
    mavenDeployer {
      repository(url: "file://$buildDir/maven")
      pom.artifactId = 'gradle-plugin'
      pom.groupId = 'org.openstreetmap.josm'
    }
  }
  doLast {
    println "Version " + repositories.mavenDeployer.pom.version + " is now deployed!"
  }
}
