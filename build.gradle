import org.gradle.api.tasks.Upload
import org.gradle.api.tasks.javadoc.Groovydoc

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'eclipse'

dependencies {
  compile gradleApi()
  compile localGroovy()
}

groovydoc {
  links = [
    new Groovydoc.Link('https://docs.gradle.org/current/javadoc/', 'org.gradle'),
    new Groovydoc.Link('https://docs.oracle.com/javase/8/docs/api/', 'java', 'javax'),
    new Groovydoc.Link('http://docs.groovy-lang.org/latest/html/api/', 'groovy')
  ]
}

def versionStream = new ByteArrayOutputStream()
exec {
  commandLine 'git', 'describe', '--dirty', '--always'
  standardOutput versionStream
}

version = versionStream.toString().trim()

task deploy(type: Upload) {
  configuration = configurations.archives
  repositories {
    mavenDeployer {
      repository(url: "file://$buildDir/maven")
      pom.version = project.version.charAt(0) == 'v' ? project.version.substring(1) : project.version
      pom.artifactId = 'gradle-plugin'
      pom.groupId = 'org.openstreetmap.josm'
    }
  }
  doLast {
    println "Version " + repositories.mavenDeployer.pom.version + " is now deployed!"
  }
}
